dnl Process this file with autoconf to produce a configure script.

AC_INIT(lsspriv.h)

AC_SUBST(rs_prefix)
AC_SUBST(prefix)
AC_SUBST(rs)
AC_SUBST(zipper_files)
AC_SUBST(other_libs)

AC_PROG_CC
AC_PROG_CPP

AC_CANONICAL_HOST

if test "$with_rs" = ""
then with_rs=rs
fi
rs="$with_rs"

rs_prefix="`$rs --install`"

if test "$prefix" = "NONE"
then prefix="$rs_prefix"
fi

echo "prefix=$prefix"

dnl -------------------------------------------------------------------------
dnl  Detect which compression methods we can link in.
dnl -------------------------------------------------------------------------

zipper_files="zipnull.c"
other_libs=""

echo "building list of compression methods"
AC_CHECK_LIB(z,compress,[LIBS="-lz $LIBS"
zipper_files="$zipper_files zipzlib.c"
other_libs="$other_libs \"z\""])

if test -z "$zipper_files"
then echo "compression methods are from... (no files)"
else echo "compression methods are from... $zipper_files"
fi

dnl -------------------------------------------------------------------------
dnl  Generate the "ziplist.ci" file, which contains the master table
dnl  of built-in compression methods.
dnl -------------------------------------------------------------------------

echo "creating ziplist.ci"

cat > ziplist.ci << EOF
/*  List of statically linked LSS zip algorithms
 *  -- generated by ./configure on `date`
 */

EOF

tmp=ziplist.tmp

for f in $zipper_files
do awk '$1 == "/*|" && $2 == "define-zip-algorithm" { print $3, $4 }' $f \
   | while read name struct 
   do echo $f $name $struct 
   done
done > $tmp

n=0

while read f name struct
do echo "extern zip_algorithm $struct; /* [$n] - \"$name\" in $f */" >> ziplist.ci
   n=`expr $n + 1`
   echo "  including \"$name\" from $f"
done < $tmp

dnl -- make room for the NULL at the end of master_table[]
n=`expr $n + 1`

changequote(|,|)dnl
cat >> ziplist.ci << EOF

static zip_algorithm *(master_table[$n]) = {
EOF
changequote([,])dnl

while read f name struct
do echo "  &$struct, /* \"$name\" in $f */"
done < $tmp >> ziplist.ci

rm -f $tmp

cat >> ziplist.ci << EOF
  NULL
};

EOF

AC_CONFIG_HEADER(config.h)
AC_CONFIG_SUBDIRS(timebase)
AC_OUTPUT(Makefile glue.scm)
